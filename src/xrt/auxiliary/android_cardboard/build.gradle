// Copyright 2020-2025, Collabora, Ltd.
// SPDX-License-Identifier: BSL-1.0

plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'com.diffplug.spotless'
}

android {
    compileSdk project.sharedCompileSdk
    buildToolsVersion = buildToolsVersion

    defaultConfig {
        minSdkVersion 24
        targetSdkVersion project.sharedTargetSdk
    }

    buildTypes {
        release {
            minifyEnabled false
            // Gradle plugin produces proguard-android-optimize.txt from @Keep annotations
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17
    }
    packagingOptions {
        resources {
            excludes += ['META-INF/*.kotlin_module']
        }
    }
    namespace 'org.freedesktop.monado.auxiliary.cardboard'
    lint {
        fatal 'StopShip'
    }
    buildFeatures {
        viewBinding true
    }
}

dependencies {
    implementation "androidx.appcompat:appcompat:$androidxAppCompatVersion"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    implementation "androidx.constraintlayout:constraintlayout:$androidxConstraintLayoutVersion"
    implementation "com.google.android.material:material:$materialVersion"
    implementation "com.github.markusfisch:BarcodeScannerView:$barcodeScannerViewVersion"
    implementation 'com.google.dagger:hilt-android:2.51.1'
}

spotless {
    java {
        target 'src/main/java/**/*.java'
        // apply a specific flavor of google-java-format.
        googleJavaFormat('1.18.1').aosp().reflowLongStrings()
        // fix formatting of type annotations.
        formatAnnotations()
    }

    kotlin {
        target 'src/main/java/**/*.kt'
        // Use ktfmt(https://github.com/facebook/ktfmt) as the default Kotlin formatter.
        ktfmt("$ktfmtVersion").kotlinlangStyle()
    }
}
